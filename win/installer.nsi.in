!include MUI2.nsh

!define SRC_PATH "@CMAKE_SOURCE_DIR@"
!define BIN_PATH "@CMAKE_BINARY_DIR@"
!define QT_PATH "@QT_BINARY_DIR@"
!define LIBAV_PATH "@WIN32_LIBAV_DIR@\bin"
!define LIBOPENSSL_PATH "C:\OpenSSL-Win32\bin"
!define WINDOWS_DLL_PATH "C:\Windows\System32"
!define MPLAYER_PATH "@CMAKE_SOURCE_DIR@\mplayer-bin\win"

Name "Bluecherry Client"
OutFile "SetupBluecherryClient.exe"
InstallDir "$PROGRAMFILES\Bluecherry Client"
InstallDirRegKey HKLM "Software\Bluecherry Client" ""
RequestExecutionLevel admin
SetCompressor /FINAL /SOLID lzma
!define MUI_ICON "${SRC_PATH}\res\bluecherry.ico"
!define MUI_UNICON "${SRC_PATH}\res\bluecherry.ico"

!insertmacro MUI_PAGE_WELCOME
#!insertmacro MUI_PAGE_LICENSE "${NSISDIR}\Docs\Modern UI\License.txt"
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_RUN $INSTDIR\BluecherryClient.exe
!define MUI_FINISHPAGE_RUN_TEXT "Run Bluecherry Client"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

Section
        SetOutPath "$INSTDIR"
        SetShellVarContext all

        # Main
        File /oname=BluecherryClient.exe "${BIN_PATH}\bluecherry-client.exe"

        # Qt
        File "${QT_PATH}\QtCore4.dll"
        File "${QT_PATH}\QtGui4.dll"
        File "${QT_PATH}\QtNetwork4.dll"
        File "${QT_PATH}\QtWebkit4.dll"
        File "${QT_PATH}\QtOpenGL4.dll"
        File "${QT_PATH}\QtDeclarative4.dll"
        File "${QT_PATH}\QtScript4.dll"
        File "${QT_PATH}\QtSql4.dll" # required by QtDeclarative
        File "${QT_PATH}\QtXmlPatterns4.dll" # QtDeclarative
        File "${QT_PATH}\QtXml4.dll" # QtDeclarative

        SetOutPath "$INSTDIR\imageformats"
        File "${QT_PATH}\..\plugins\imageformats\qjpeg4.dll"
        File "${QT_PATH}\..\plugins\imageformats\qgif4.dll"
        SetOutPath "$INSTDIR"

        # Translations
        File "${BIN_PATH}\bluecherryclient_de_DE.qm"
        File "${BIN_PATH}\bluecherryclient_en.qm"
        File "${BIN_PATH}\bluecherryclient_es.qm"
        File "${BIN_PATH}\bluecherryclient_no.qm"
        File "${BIN_PATH}\bluecherryclient_ru_RU.qm"
        File "${BIN_PATH}\bluecherryclient_sv_SE.qm"
		
        # ffmpeg
        File "${LIBAV_PATH}\avcodec-55.dll"
        File "${LIBAV_PATH}\avformat-55.dll"
        File "${LIBAV_PATH}\avutil-52.dll"
        File "${LIBAV_PATH}\swscale-2.dll"
        #File "${LIBAV_PATH}\libmp3lame-0.dll"
        #File "${LIBAV_PATH}\libvo-aacenc-0.dll"
        #File "${LIBAV_PATH}\libvorbis-0.dll"
        #File "${LIBAV_PATH}\libogg-0.dll"
        #File "${LIBAV_PATH}\libvorbisenc-2.dll"
        #File "${LIBAV_PATH}\libx264-125.dll"

        SetOutPath "$INSTDIR"
		# mplayer
		File "${MPLAYER_PATH}\mplayer.exe"

        # OpenSSL
        File "${LIBOPENSSL_PATH}\ssleay32.dll"
        File "${LIBOPENSSL_PATH}\libeay32.dll"

        # MSVC 2010 CRT
        File "${WINDOWS_DLL_PATH}\msvcp100.dll"
        File "${WINDOWS_DLL_PATH}\msvcr100.dll"

        CreateDirectory "$SMPROGRAMS\Bluecherry"
        CreateShortCut "$SMPROGRAMS\Bluecherry\Bluecherry Client.lnk" "$INSTDIR\BluecherryClient.exe"

        WriteRegStr HKLM "Software\Bluecherry Client" "" $INSTDIR
        WriteUninstaller "$INSTDIR\Uninstall.exe"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "DisplayName" "Bluecherry DVR Client"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "Publisher" "Bluecherry"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "HelpLink" "http://support.bluecherrydvr.com"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "URLInfoAbout" "http://www.bluecherrydvr.com"
SectionEnd

Section "Uninstall"
        SetShellVarContext all
        Delete "$INSTDIR\Uninstall.exe"
        RMDir /r "$INSTDIR"
        RMDir /r "$SMPROGRAMS\Bluecherry"
        DeleteRegKey HKLM "Software\Bluecherry Client"
        DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client"
SectionEnd
