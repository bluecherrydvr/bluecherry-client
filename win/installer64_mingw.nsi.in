!include MUI2.nsh

!define SRC_PATH "@CMAKE_SOURCE_DIR@"
!define BIN_PATH "@CMAKE_BINARY_DIR@"
!define LIBAV_PATH "@WIN32_LIBAV_DIR@\bin"
!define LIBOPENSSL_PATH "@WIN32_OPENSSL_DIR@\bin"
!define LIBSDL2_PATH "@WIN32_LIBSDL2_DIR@\bin"
!define WINDOWS_DLL_PATH "C:\Windows\System32"
!define MINGW_SYSROOT_PATH "@MINGW_SYSROOT_PATH@"
!define QT_PATH "@MINGW_SYSROOT_PATH@\bin"
!define MPLAYER_PATH "@CMAKE_SOURCE_DIR@\mplayer-bin\win\amd64"
!define VERSION "@VERSION@"

Name "Bluecherry Client"
OutFile "SetupBluecherryClient_amd64_${VERSION}.exe"
InstallDir "$PROGRAMFILES\Bluecherry Client"
InstallDirRegKey HKLM "Software\Bluecherry Client" ""
RequestExecutionLevel admin
SetCompressor /FINAL /SOLID lzma
!define MUI_ICON "${SRC_PATH}\res\bluecherry.ico"
!define MUI_UNICON "${SRC_PATH}\res\bluecherry.ico"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${SRC_PATH}\COPYING"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_RUN $INSTDIR\BluecherryClient.exe
!define MUI_FINISHPAGE_RUN_TEXT "Run Bluecherry Client"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

Section
        SetOutPath "$INSTDIR"
        SetShellVarContext all

        # Main
        File /oname=BluecherryClient.exe "${BIN_PATH}\bluecherry-client.exe"

        #License
        File "${SRC_PATH}\COPYING"

        # Qt
        File "${QT_PATH}\QtCore4.dll"
        File "${QT_PATH}\QtGui4.dll"
        File "${QT_PATH}\QtNetwork4.dll"
        File "${QT_PATH}\QtWebkit4.dll"
        File "${QT_PATH}\QtOpenGL4.dll"
        File "${QT_PATH}\QtDeclarative4.dll"
        File "${QT_PATH}\QtScript4.dll"
        File "${QT_PATH}\QtSql4.dll" # required by QtDeclarative
        File "${QT_PATH}\QtXmlPatterns4.dll" # QtDeclarative
        File "${QT_PATH}\QtXml4.dll" # QtDeclarative

        SetOutPath "$INSTDIR\imageformats"
        File "${QT_PATH}\..\plugins\imageformats\qjpeg4.dll"
        File "${QT_PATH}\..\plugins\imageformats\qgif4.dll"
        SetOutPath "$INSTDIR"

        # Translations
        File "${BIN_PATH}\bluecherryclient_de_DE.qm"
        File "${BIN_PATH}\bluecherryclient_en.qm"
        File "${BIN_PATH}\bluecherryclient_es.qm"
        File "${BIN_PATH}\bluecherryclient_no.qm"
        File "${BIN_PATH}\bluecherryclient_pl.qm"
        File "${BIN_PATH}\bluecherryclient_ru.qm"
        File "${BIN_PATH}\bluecherryclient_sv_SE.qm"
		
        # ffmpeg
        File "${LIBAV_PATH}\avcodec-57.dll"
        File "${LIBAV_PATH}\avformat-57.dll"
        File "${LIBAV_PATH}\avutil-55.dll"
        File "${LIBAV_PATH}\swscale-4.dll"
        File "${LIBAV_PATH}\swresample-2.dll"

        SetOutPath "$INSTDIR"
		# mplayer
		File "${MPLAYER_PATH}\mplayer.exe"

        # OpenSSL
        File "${LIBOPENSSL_PATH}\ssleay32.dll"
        File "${LIBOPENSSL_PATH}\libeay32.dll"
        # SDL2
        File "${LIBSDL2_PATH}\SDL2.dll"

        # mingw-w64 runtime
        File "${MINGW_SYSROOT_PATH}\bin\libgcc_s_seh-1.dll"
        File "${MINGW_SYSROOT_PATH}\bin\libstdc++-6.dll"

	# additional dependencies
	File "${MINGW_SYSROOT_PATH}\bin\zlib1.dll"
	File "${MINGW_SYSROOT_PATH}\bin\libpng16-16.dll"
	File "${MINGW_SYSROOT_PATH}\bin\libsqlite3-0.dll"
	File "${MINGW_SYSROOT_PATH}\bin\libwebp-5.dll"
	File "${MINGW_SYSROOT_PATH}\bin\libxslt-1.dll"
	File "${MINGW_SYSROOT_PATH}\bin\libxml2-2.dll"
	File "${MINGW_SYSROOT_PATH}\bin\iconv.dll"
	File "${MINGW_SYSROOT_PATH}\bin\liblzma-5.dll"

        CreateDirectory "$SMPROGRAMS\Bluecherry"
        CreateShortCut "$SMPROGRAMS\Bluecherry\Bluecherry Client.lnk" "$INSTDIR\BluecherryClient.exe"

        WriteRegStr HKLM "Software\Bluecherry Client" "" $INSTDIR
        WriteUninstaller "$INSTDIR\Uninstall.exe"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "DisplayName" "Bluecherry DVR Client"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "Publisher" "Bluecherry"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "HelpLink" "http://support.bluecherrydvr.com"
        WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client" \
                    "URLInfoAbout" "http://www.bluecherrydvr.com"
SectionEnd

Section "Uninstall"
        SetShellVarContext all
        Delete "$INSTDIR\Uninstall.exe"
        RMDir /r "$INSTDIR"
        RMDir /r "$SMPROGRAMS\Bluecherry"
        DeleteRegKey HKLM "Software\Bluecherry Client"
        DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Bluecherry Client"
SectionEnd
